\title{Lab4 7-Segment LED}
\author{0616069 張晉瑋, 0616014 楊政道}
\maketitle
\thispagestyle{fancy}
\section{Objectives}
\paragraph{}
With the help of GPIO, we are able to craft variable signals for external devices.
In this lab, the MAX7219 LED display driver runs the role.
We're going to control GPIO pins to communicate with MAX7219, and display the messages assigned in the spec
(a counter, displaying student ID and showing fibonacci sequence controlled by a button).

\section{Experiment Procedure}
\subsection{Initialization and Communication}
\paragraph{}
First of all, we need to communicate with MAX7219 in order to control it. The communication between microprocessor and MAX7219 by 3 pins, one of them is data input, another is clock and the other is end-of-input controller. A legal communication between microprocessor and MAX7219 consist of 16 bits data. MAX7219 will read one bit when the clock in the positive edge triggered. Therefore, we can set the clock into 0 first, set data pin into correct bit and set the clock into 1. MAX7219 will receive one bit and repeat it 16 times. Finally, set end-of-input pin up and down to complete the communication. The code will be listed below as a reference.
\begin{lstlisting}
MAX7219Send:
    //  Input: r0, r1. response to message X send to MAX7219. X[0-7]: r1, X[8-15]: r0
    push    {r0,    r1,     r2,     lr}
    lsl     r0,     r0,     #8
    orr     r0,     r0,     r1
    rbit    r0,     r0
    lsr     r0,     #16
    mov     r1,     #16
MAX7219SendLoop:
    ldr     r2,     =CLOCK
    bl      BitReset
    ldr     r2,     =DATA
    tst     r0,     #1
    it      ne
    blne    BitSet
    it      eq
    bleq    BitReset
    ldr     r2,     =CLOCK
    bl      BitSet
    lsr     r0,     r0,     #1
    subs    r1,     r1,     #1
    bne     MAX7219SendLoop
    ldr     r2,     =LOAD
    bl      BitSet
    bl      BitReset
    pop     {r0,    r1,     r2,     pc}

BitSet:
    push    {r0,    r1,     lr}
    ldr     r0,     =GPIOB_BASE
    ldr     r1,     =GPIO_BSRR_OFFSET
    str     r2,     [r0,    r1]
    pop     {r0,    r1,     pc}

BitReset:
    push    {r0,    r1,     lr}
    ldr     r0,     =GPIOB_BASE
    ldr     r1,     =GPIO_BRR_OFFSET
    str     r2,     [r0,    r1]
    pop     {r0,    r1,     pc}
\end{lstlisting}
\paragraph{}
We can use this function to send a 16bit message to MAX7219 and setup the MAX7219 conveniently. There are some register on MAX7219 we need to set in order to make it work properly.
\begin{lstlisting}
MAX7219Init:
    push    {lr}
    bl      SetMAX7219_DECODE_MODE
    bl      SetMAX7219_DISPLAY_TEST
    bl      SetMAX7219_SCAN_LIMIT
    bl      SetMAX7219_INTENSITY
    bl      SetMAX7219_SHUTDOWN
    bl      ResetMAX7219Digit
    pop     {pc}
\end{lstlisting}
\subsection{Display0toF}
\paragraph{}
In this section, we need to implement a counter counting 0 to F with 1 second delay between each digit. 
\paragraph{}
First of all, we need to initialize MAX7219.
\begin{lstlisting}
SetMAX7219_DECODE_MODE:
    push    {r0,    r1,     lr}
    ldr     r0,     =DECODE_MODE
    ldr     r1,     =#0x00
    bl      MAX7219Send
    pop     {r0,    r1,     pc}

SetMAX7219_DISPLAY_TEST:
    push    {r0,    r1,     lr}
    ldr     r0,     =DISPLAY_TEST
    ldr     r1,     =#0x00
    bl      MAX7219Send
    pop     {r0,    r1,     pc}

SetMAX7219_SCAN_LIMIT:
    push    {r0,    r1,     lr}
    ldr     r0,     =SCAN_LIMIT
    ldr     r1,     =#0x00
    bl      MAX7219Send
    pop     {r0,    r1,     pc}

SetMAX7219_INTENSITY:
    push    {r0,    r1,     lr}
    ldr     r0,     =INTENSITY
    ldr     r1,     =#0x0A
    bl      MAX7219Send
    pop     {r0,    r1,     pc}

SetMAX7219_SHUTDOWN:
    push    {r0,    r1,     lr}
    ldr     r0,     =#SHUTDOWN
    ldr     r1,     =#0x1
    bl      MAX7219Send
    pop     {r0,    r1,     pc}
\end{lstlisting}
Because we need to display A to F, the decode mode in MAX7219 doesn't support the english digit. Therefore we must control the 7 segment display in non-decode mode, and set each led on or off seperately.
The table of each digit and their corresponding led configuration(D7~D0) is given below:
\begin{table}[h]
\centering
\begin{tabular}{|l|l|l|l|l|l|l|l|l|l|}
\hline
  & D7 & D6 & D5 & D4 & D3 & D2 & D1 & D0 &     \\ \hline
  & P  & A  & B  & C  & d  & E  & F  & G  & HEX \\ \hline
0 & 0  & 1  & 1  & 1  & 1  & 1  & 1  & 0  & 7E  \\ \hline
1 & 0  & 0  & 1  & 1  & 0  & 0  & 0  & 0  & 30  \\ \hline
2 & 0  & 1  & 1  & 0  & 1  & 1  & 0  & 1  & 6D  \\ \hline
3 & 0  & 1  & 1  & 1  & 1  & 0  & 0  & 1  & 79  \\ \hline
4 & 0  & 0  & 1  & 1  & 0  & 0  & 1  & 1  & 33  \\ \hline
5 & 0  & 1  & 0  & 1  & 1  & 0  & 1  & 1  & 5B  \\ \hline
6 & 0  & 1  & 0  & 1  & 1  & 1  & 1  & 1  & 5F  \\ \hline
7 & 0  & 1  & 1  & 1  & 0  & 0  & 0  & 0  & 70  \\ \hline
8 & 0  & 1  & 1  & 1  & 1  & 1  & 1  & 1  & 7F  \\ \hline
9 & 0  & 1  & 1  & 1  & 1  & 0  & 1  & 1  & 7B  \\ \hline
A & 0  & 1  & 1  & 1  & 0  & 1  & 1  & 1  & 77  \\ \hline
b & 0  & 0  & 0  & 1  & 1  & 1  & 1  & 1  & 1F  \\ \hline
C & 0  & 1  & 0  & 0  & 1  & 1  & 1  & 0  & 4E  \\ \hline
d & 0  & 0  & 1  & 1  & 1  & 1  & 0  & 1  & 3D  \\ \hline
E & 0  & 1  & 0  & 0  & 1  & 1  & 1  & 1  & 4F  \\ \hline
F & 0  & 1  & 0  & 0  & 0  & 1  & 1  & 1  & 47  \\ \hline
\end{tabular}
\end{table}
\paragraph{}
Finally, we just iterate through this 16 state and display them with 1 second delay.
\begin{lstlisting}
Display0toF:
    push    {r0,    r1,     r2,     r3,     lr}
    ldr     r2,     =arr
    mov     r0,     #1
    mov     r3,     #0
Display0toFLoop:
    ldrb    r1,     [r2,    r3]
    bl      MAX7219Send
    bl      Delay
    add     r3,     r3,     #1
    cmp     r3,     #16
    bne     Display0toFLoop
    pop     {r0,    r1,     r2,     r3,     pc}

Delay:
    push    {r0,    lr}
    ldr     r0,     =delay_counter
    ldr     r0,     [r0]
DelayLoop:
    subs    r0,     r0,     #1
    bne     DelayLoop
    pop     {r0,    pc}
\end{lstlisting}
\subsection{Display Student ID}
\paragraph{}
In this lab, we need to set the value of the decode-mode register to \texttt{0xFF} for MAX7219 to decode all the digits.
Besides, the value of scan-limit register should also be set to \texttt{0x06} to diaplying the digits.
\paragraph{}
Since only displaying ID is required, we run a single loop to send data for each digit and then get into an dummy infinite loop.
In the display loop, we treat \texttt{r2} as base and \texttt{r3} as offset, and load byte from address \texttt{[r2, r3]}. 
We also increment address field for MAX7219Send and the offset to load, and they can be stored in \texttt{r0} and \texttt{r1} as parameters for MAX7219Send.
The code below shows the main program and the display loop:
\begin{lstlisting}
main:
    bl      Init
    bl      Display
Loop:
    b       Loop

Display:
    push    {r0,    r1,     r2,     lr}
    ldr     r2,     =student_id
    mov     r3,     #0
    mov     r0,     #7
DisplayLoop:
    ldrb    r1,     [r2,    r3]
    bl      MAX7219Send
    sub     r0,     r0,     #1
    add     r3,     r3,     #1
    cmp     r3,     #7
    bne     DisplayLoop
    pop     {r0,    r1,     r2,     pc}
\end{lstlisting}
\subsection{Fibonacci Sequence with Button Control}
\paragraph{}
In this part, there comes the input, and we also need conversion from binary numbers to the decimal result in our method.
In the main loop, we firstly update the debounce counter(btn\_counter) in label \texttt{ReadBtn},
then determine whether should it update the displayed value or not.
If so, we call the \texttt{Display} subroutine, which checks that the value is valid,
update the value in variable \texttt{fib1}, then display the value in stored in it.
\paragraph{}
In \texttt{ReadBtn}, we add the counter and update the button-pushed signal,
which may either set \texttt{fib\_chg} to \texttt{1} or trigger \texttt{FibReset} subroutine based on the counter value.
This part is basically similar to the push button reading in lab3. Below show some difference that contains detection of 1-second push:
\begin{lstlisting}
HandleBtnSignal:
    ldr     r0,     =btn_counter
    ldr     r1,     [r0]
    ldr     r0,     =btn_bounce_limit
    ldr     r2,     [r0]
    cmp     r1,     r2
    blt     ReadBtnEnd
    ldr     r0,     =btn_prev
    ldr     r1,     [r0]
    cmp     r1,     #0
    beq     BtnBePushedSignal
    bne     BtnUnPushedSignal
BtnBePushedSignal:
    ldr     r0,     =btn_counter
    ldr     r1,     [r0]
    ldr     r0,     =btn_reset_limit    // <-- This is larger than btn_bounce_limit and close to one second duration when being counted up to it.
    ldr     r2,     [r0]
    cmp     r1,     r2
    bgt     FibReset
    ldr     r0,     =btn_chg
    ldr     r1,     [r0]
    cmp     r1,     #1
    beq     ReadBtnEnd
    mov     r1,     #1
    str     r1,     [r0]
    ldr     r0,     =fib_chg
    mov     r1,     #1
    str     r1,     [r0]
    b       ReadBtnEnd
\end{lstlisting}
\paragraph{}
For the display part, we check if the current number is zero or greater than our diaplay limit, then compute the next fibonacci number.
After that we display the number in the \texttt{DisplayNumber} label.
This subroutine obtains each decimal digit in the number by keeping divide it by 10 and get the remainder,
then display the number at the corresponding position.
Below is the code for \texttt{DisplayNumber} which displays non-zero value in register \texttt{r3}
\begin{lstlisting}
DisplayNumber:
    mov     r0,     #1
    mov     r2,     #10
    ldr     r3,     =fib1
    ldr     r3,     [r3]
DisplayNumberLoop:
    cmp     r3,     #0
    beq     DisplayEnd
    sdiv    r1,     r3,     r2
    mul     r1,     r1,     r2
    sub     r1,     r3,     r1
    sdiv    r3,     r3,     r2
    bl      MAX7219Send
    add     r0,     r0,     #1
    b       DisplayNumberLoop
    
DisplayEnd:
    pop     {r0,    r1,     r2,     r3,     pc}
\end{lstlisting}
\section{Feedback}
\paragraph{}
During our experiments, we found that if we don't clear out the register of MAX7219(or don't switch off its power completely),
The values of the registers will be messed up.
For such situation, we set up almost every registers explicitly in the initialization code.
It made the code tedious while improved our understanding about MAX7219 driver.
